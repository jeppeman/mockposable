apply plugin: 'maven-publish'
apply plugin: 'signing'

group = GROUP
version = VERSION_NAME

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL')
            ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL')
            ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('SONATYPE_NEXUS_USERNAME') ? SONATYPE_NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('SONATYPE_NEXUS_PASSWORD') ? SONATYPE_NEXUS_PASSWORD : ""
}

afterEvaluate {
    if (project.getPlugins().hasPlugin('com.android.application') ||
            project.getPlugins().hasPlugin('com.android.library')) {

        task androidSourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.source
        }

        publishing {
            publications {
                android.libraryVariants.findAll { it.baseName.contains("release") }.collect { variant ->
                    "${variant.name}Aar"(MavenPublication) {
                        version = version
                        groupId = GROUP

                        from components.findByName(variant.name)
                        artifact project.androidSourcesJar

                        pom {
                            name = POM_NAME
                            description = POM_DESCRIPTION
                            url = POM_URL

                            scm {
                                url = POM_SCM_URL
                                connection = POM_SCM_CONNECTION
                                developerConnection = POM_SCM_DEV_CONNECTION
                            }

                            licenses {
                                license {
                                    name = POM_LICENCE_NAME
                                    url = POM_LICENCE_URL
                                    distribution = POM_LICENCE_DIST
                                }
                            }

                            developers {
                                developer {
                                    id = POM_DEVELOPER_ID
                                    name = POM_DEVELOPER_NAME
                                }
                            }
                        }

                        if (variant.name == "release") {
                            alias false
                            artifactId = POM_ARTIFACT_ID
                        } else {
                            alias true
                            artifactId = "$POM_ARTIFACT_ID-${variant.name}"

                            pom.withXml {
                                asNode().children().find { node ->
                                    node.name().localPart == "dependencies"
                                }.children().each { node ->
                                    def groupId = node.get("groupId").first().value().join()
                                    if (groupId == GROUP) {
                                        def artifact = node.get("artifactId").first()
                                        def artifactValue = artifact.value().join().trim()
                                        def artifactProject = rootProject.subprojects.find { subproject ->
                                            subproject.hasProperty("POM_ARTIFACT_ID") && subproject.POM_ARTIFACT_ID == artifactValue
                                        }

                                        if (artifactProject != null && (artifactProject.getPlugins().hasPlugin('com.android.application') ||
                                                artifactProject.getPlugins().hasPlugin('com.android.library'))) {
                                            artifact.setValue(["$artifactValue-${variant.name}"])
                                        }

                                    }
                                }
                            }
                        }
                    }
                }
            }
        }

        if (isReleaseBuild()) {
            signing {
                required { isReleaseBuild() && gradle.taskGraph.hasTask("publish") }
                sign publishing.publications.findAll { it.name.endsWith("Aar") } as Publication[]
            }
        }
    } else {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        tasks.withType(GenerateMavenPom) { task ->
            doFirst {
                def pom = task.pom

                pom.name = POM_NAME
                pom.url = POM_URL
                pom.description = POM_DESCRIPTION

                pom.scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }

                pom.licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                        distribution = POM_LICENCE_DIST
                    }
                }

                pom.developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                    }
                }
            }
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifact sourcesJar
                    artifact javadocJar

                    from components.kotlin

                    groupId = GROUP
                    artifactId = POM_ARTIFACT_ID
                    version = version

                    pom {
                        packaging = POM_PACKAGING
                    }
                }
            }
        }

        signing {
            required { isReleaseBuild() && gradle.taskGraph.hasTask("publish") }
            sign publishing.publications
        }
    }

    publishing {
        repositories {
            maven {
                url isReleaseBuild() ? getReleaseRepositoryUrl() : getSnapshotRepositoryUrl()
                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
        }
    }
}